#+title: GNU Emacs configuration/distribution
#+author: Klementiev Dmitry
#+email: klementievd08@yandex.ru

Main goals of this distribution:
- Convenient navigation through the =Emacs.org=
- Fast startup (also with Org Babel)
- Good Emacs Lisp code. Keep it simple to read and maintain
- Only a few packages are loaded at startup (lazy-loading)
- No =use-package= macro. You know what you're doing.
- Excellent terminal support (=emacs -nw=)


Purposes of my GNU Emacs:
- Lisp Development (Emacs Lisp, Common Lisp)
- Backend Development
- Organizing, Planning


This configuration heavily inspired by following things:
- RDE (GNU Guix based distribution with Emacs configuration)
- Doom Emacs (GNU Emacs distribution)
- Spacemacs (Another GNU Emacs distribution)


Other files:
- [[file:early-init.el][Early Initialization file]]

  Main optimizations of GNU Emacs.

- [[file:init.el][Initialization file]]

  Bootstrap Elpaca and load Org Babel files.


Use lexical-binding. (for byte-compilation)
#+begin_src emacs-lisp
  ;;; -*- lexical-binding: t; -*-
#+end_src

* Table Of Contents :TOC:
- [[#functions-and-macroses][Functions and Macroses]]
  - [[#after-macro][=after!= macro]]
  - [[#require-on-macro][=require-on!= macro]]
  - [[#autoload-macro][=autoload!= macro]]
- [[#basic-emacs][Basic Emacs]]
- [[#uiux][UI/UX]]
  - [[#font-and-font-size][Font and Font Size]]
  - [[#theme][Theme]]
  - [[#hl-todo-highlight-todos][HL-TODO (Highlight TODOs)]]
  - [[#initial-screen-dashboard][Initial Screen (Dashboard)]]
  - [[#golden-ratio-for-emacs-windows][Golden Ratio for Emacs windows]]
  - [[#tab-bar-mode][Tab Bar mode]]
- [[#navigation][Navigation]]
  - [[#dired][Dired]]
  - [[#which-key][Which-key]]
  - [[#hydra][Hydra]]
  - [[#vertico][Vertico]]
  - [[#marginalia][Marginalia]]
  - [[#consult][Consult]]
  - [[#embark][Embark]]
  - [[#avy][Avy]]
  - [[#ace-window][Ace-Window]]
- [[#editing][Editing]]
  - [[#smartparens][Smartparens]]
  - [[#git-gutter][Git-Gutter]]
  - [[#tree-sitter][Tree-Sitter]]
- [[#completion][Completion]]
  - [[#orderless][Orderless]]
  - [[#corfu][Corfu]]
- [[#org-mode][Org Mode]]
  - [[#main-settings][Main Settings]]
  - [[#planning-agenda][Planning (Agenda)]]
  - [[#note-taking-roam][Note Taking (Roam)]]
- [[#development][Development]]
  - [[#vcs-magit-forge-git-modes][VCS (Magit, Forge, Git-Modes)]]
  - [[#repl-eat-eshell][REPL (Eat, Eshell)]]
  - [[#languages][Languages]]
- [[#tools][Tools]]
  - [[#leetcode][LeetCode]]
  - [[#google-translate][Google Translate]]
  - [[#syncthing][Syncthing]]

* Functions and Macroses

Набор функций и макросов для конфигурации.

** =after!= macro

Более удобная и лучшая версия =with-eval-after-load=.

#+begin_src emacs-lisp
  (defmacro after! (packages &rest body)
    (declare (indent defun))
    (let ((packages (eval `(,@packages))))
      (cond ((not packages)
             `(progn
                ,@body))
            ((symbolp packages)
             `(with-eval-after-load ',packages
                ,@body))
            ((listp packages)
             `(with-eval-after-load ',(car packages)
                (after! ',(cdr packages)
                  ,@body))))))
#+end_src

** =require-on!= macro

Предоставляет возможность подгружать пакет при использовании хука или комманды.

По факту это добавляет хук или "advice" который запускается лишь единожды, подгружая пакет.

#+begin_src emacs-lisp
  (defun require-on-hook (hook package)
    (let ((fnname (intern (concat "load-" (prin1-to-string package)
                                  "-on-" (prin1-to-string hook)))))
      `(when (boundp ',hook)
         ,(unless (fboundp fnname)
            `(defun ,fnname (&rest _)
               (require ',package)
               (remove-hook ',hook #',fnname)))
         (add-hook ',hook #',fnname))))

  (defun require-on-command (command package)
    (let ((fnname (intern (concat "load-" (prin1-to-string package)))))
      (when (fboundp command)
        `(define-advice ,command
             (:before (&rest _) ,fnname)
           (advice-remove ',command #',(intern (concat (prin1-to-string command)
                                                       "@" (prin1-to-string fnname))))
           (require ',package)))))

  (defmacro require-on! (symbol package)
    "Load PACKAGE on SYMBOL."
    (declare (indent nil))
    (cond
     ((commandp symbol)
      (require-on-command symbol package))
     ((boundp symbol)
      (require-on-hook symbol package))
     (t (error "Can't determine symbol for require-on!: %s" symbol))))
#+end_src

** =autoload!= macro

Удобный макрос для ленивой загрузки. В отличие от обычной функции =autoload= можно загружать
несколько функций одним вызовом макроса (в целом это и есть всё удобство).

#+begin_src emacs-lisp
  (defun autoload! (package &rest autoloads)
    "Generate AUTOLOADS from PACKAGE by repeating `autoload' function."
    (declare (indent defun))
    (dolist (symbol autoloads)
      (cond ((listp symbol)
             (pcase-let* ((`(,symbol ,doc-string ,interactive?) symbol))
               (autoload symbol package doc-string interactive?)))
            ((symbolp symbol)
             (autoload symbol package))
            (t (error "Can't determine symbol type for autoloading with `autoload!' macro")))))
#+end_src

* Basic Emacs

#+begin_src emacs-lisp
  (defun load-my-custom-file ()
    (load custom-file :no-error :no-message :no-suffix :must-suffix))

  (after! 'emacs
    (add-hook 'elpaca-after-init-hook #'load-my-custom-file)

    (setq frame-title-format "GNU Emacs"
          make-backup-files nil
          custom-file (expand-file-name "custom.el" user-emacs-directory)
          cursor-type 'box
          ;; cursor-type '(bar . 2)
          cursor-in-non-selected-windows nil
          default-input-method "russian-computer"
          indent-tabs-mode nil
          tab-width 4)

    (blink-cursor-mode 1)
    (add-hook 'text-mode-hook 'visual-line-mode)
    (add-hook 'prog-mode-hook 'visual-line-mode))

  (eval-when-compile
    (require 'recentf)
    (require 'fringe))

  (after! 'recentf
    (recentf-mode 1))

  (after! 'fringe
    (fringe-mode '(8 . 8)))
#+end_src

* UI/UX

*Настройка UI/UX.*

Данная часть конфигурации подразумевает в себе не только красивый и приятный глазу внешний вид,
но и удобный, интуитивный интерфейс.

** Font and Font Size

#+begin_src emacs-lisp
  (defvar my-font-size 120)
  (if (daemonp)
      (add-hook 'after-make-frame-functions
  	      (lambda (frame)
  		(with-selected-frame frame
  		  (set-face-attribute 'default nil :height my-font-size))))
    (add-hook 'emacs-startup-hook
  	    (lambda ()
  	      (set-face-attribute 'default nil :height my-font-size))))
#+end_src

** Theme

Мне очень нравится обычная тема из Doom Emacs. Она слишком хороша собой :)

#+begin_src emacs-lisp
  (defvar default-theme 'doom-one-light
    "Default theme for GNU Emacs.

  1. Autoload `default-theme' from theme-package
  2. Load theme")

  (elpaca 'doom-themes
    (autoload! "doom-themes"
      default-theme)

    (if (daemonp)
        (add-hook 'after-make-frame-functions
                  (lambda (frame)
                    (with-selected-frame frame
                      (load-theme default-theme :no-confirm))))
      (add-hook 'elpaca-after-init-hook
                (lambda ()
                  (load-theme default-theme :no-confirm)))))
#+end_src

** HL-TODO (Highlight TODOs)

TODO: Add *PERF* keyword
TODO: ADD *BUG* keyword

#+begin_src emacs-lisp
  (setq-default hl-todo-require-punctuation t
  	      hl-todo-highlight-punctuation ":")

  ;; TODO: Move nerd-icons from the hl-todo
  ;; PERF: No lazy loading.
  ;;   Nerd-Icons loaded at startup time
  (elpaca 'nerd-icons
    (eval-when-compile
      (require 'nerd-icons)))

  (elpaca 'hl-todo
    (autoload! "hl-todo"
      'hl-todo-mode)
    (add-hook 'text-mode-hook 'hl-todo-mode)
    (add-hook 'prog-mode-hook 'hl-todo-mode))

  (after! 'hl-todo
    (add-hook 'hl-todo-mode-hook
  	    (lambda ()
  	      (unless hl-todo-mode
  		(remove-overlays nil nil 'hl-todo t))))
    (add-to-list 'hl-todo--keywords `(,(lambda (bound) (remove-overlays (point) bound 'hl-todo t) nil))))

  (after! '(hl-todo nerd-icons)
    (define-advice hl-todo--get-face
        (:override () with-icons)
      (let* ((keyword (match-string 2))
             (ov (make-overlay (match-beginning 0) (match-end 0))))

        ;; Overlays only for the icons
        (overlay-put ov 'hl-todo t)
        (overlay-put ov 'evaporate t)
        (overlay-put ov 'before-string
                     (pcase keyword
                       ("TODO" (nerd-icons-sucicon "nf-seti-todo"))
                       ("TEMP" (nerd-icons-mdicon "nf-md-timer"))
                       ("BUG" (nerd-icons-faicon "nf-fa-bug"))
                       ("FIXME" (nerd-icons-faicon "nf-fa-wrench"))
                       ("WARNING" (nerd-icons-faicon "nf-fa-flag"))
                       (_ (nerd-icons-mdicon "nf-md-content_paste"))))

        ;; Return color for font-lock
        (hl-todo--combine-face
         (cdr (or
               ;; Fast allocation free lookup for literal keywords.
               (assoc keyword hl-todo-keyword-faces)
               ;; Slower regexp lookup.
               (compat-call assoc keyword hl-todo-keyword-faces
                            (lambda (a b)
                              (string-match-p (format "\\`%s\\'" a) b)))))))))
#+end_src

** Initial Screen (Dashboard)

В качестве начального экрана есть два пакета:
- =dashboard= - Максимально простой и готов к использованию
- =enlight= + =grid= - Очень глубокая кастомизация (сложен в настройке)

Пока что я выберу Dashboard, и не буду париться о том, что и как нужно
делать.

#+begin_src emacs-lisp
  (setq-default dashboard-center-content t
                dashboard-vertically-center-content nil
                dashboard-items '((recents . 10)
                                  (bookmarks . 3)
                                  (projects . 3)
                                  (agenda . 5)))
  (elpaca 'dashboard
    (autoload! "dashboard"
      'dashboard-setup-startup-hook)
    (dashboard-setup-startup-hook))

  (after! 'dashboard
    (add-hook 'dashboard-mode-hook 'visual-line-mode)

    ;; Open dashboard when using "emacsclient -c" (daemon only)
    (when (daemonp)
      (setq initial-buffer-choice (lambda () (get-buffer-create dashboard-buffer-name))))

    (defun my/dashboard-init--info ()
      (format "%d packages installed. %d packages loaded. Emacs started in %s"
              (dashboard-init--packages-count)
              external-packages-loaded-count
              (dashboard-init--time)))

    (setq dashboard-init-info #'my/dashboard-init--info))
#+end_src

** Golden Ratio for Emacs windows

#+begin_src emacs-lisp
  (elpaca 'golden-ratio
    (require-on! split-window-below
                 golden-ratio)

    (require-on! split-window-right
                 golden-ratio))

  (after! 'golden-ratio
    (golden-ratio-mode 1)
    (add-to-list 'golden-ratio-extra-commands 'ace-window))
#+end_src

** Tab Bar mode

#+begin_src emacs-lisp
  (autoload! "tab-bar"
    '(tab-bar-mode nil t))

  (if (daemonp)
      (add-hook 'after-make-frame-functions
                (lambda (frame)
                  (with-selected-frame frame
                    (tab-bar-mode 1))))
    (add-hook 'emacs-startup-hook 'tab-bar-mode))
#+end_src

* Navigation

*Навигация*

Данный заголовок включает в себя не только навигацию в тексте.

Вот что подразумевается под *навигацией*:
- Навигация в буффере/тексте
- Перемещение между буфферами (=consult-buffer=)
- Перемещение между окнами
- Навигация между сочетаниями клавиш (соответственно их конфигурация: which-key, hydra, и.т.д)
- Навигация в минибуффере и между коммандами (vertico, embark, consult)


** Dired

#+begin_src emacs-lisp
  (elpaca 'dired-gitignore
    (autoload! "dired-gitignore"
      'dired-gitignore-mode)
    (add-hook 'dired-mode-hook 'dired-gitignore-mode))

  (after! '(dired dired-gitignore)
    ;; MAYBE: More convenient keybinding for
    ;;    `dired-gitignore-mode' toggle.
    (bind-key "C-d" 'dired-gitignore-mode dired-mode-map))
#+end_src

** Which-key

#+begin_src emacs-lisp
  (if after-init-time
      (which-key-mode 1)
    (add-hook 'after-init-hook 'which-key-mode))
#+end_src

** Hydra

#+begin_src emacs-lisp
  ;; FIXME: Byte-Compilation throw error when using `defhydra'
  ;;   out of `elpaca' macro. All `defhydra' usage are written
  ;;   in `elpaca' usage for `hydra' package.
  ;; PERF: Hydra loaded at startup time.
  ;; TODO: Improve Hydra configuration.
  ;;   It throw errors out of `elpaca' macro when byte-compiling.
  ;;   Also it loads at startup time. (No lazy loading)
  (elpaca 'hydra
    (require 'hydra)
    
    (defhydra hydra-zoom ()
      "Zoom"
      ("i" text-scale-increase "in")
      ("d" text-scale-decrease "out"))
    (bind-key "<f2>" 'hydra-zoom/body)
    
    (defhydra hydra-org-timer ()
      "Org Timer"
      ("s" org-timer-start "Start timer")
      ("e" org-timer-stop "Timer end (Stop timer)")
      ("p" org-timer-pause-or-continue "Pause/Continue timer")
      ("t" org-timer-set-timer "Set timer")
      ("q" nil "Quit"))
    (bind-key "o t" 'hydra-org-timer/body mode-specific-map))
#+end_src

** Vertico

#+begin_src emacs-lisp
  (elpaca 'vertico
    (require-on! pre-command-hook
                 vertico))

  (after! 'vertico
    (vertico-mode 1))
#+end_src

** Marginalia

#+begin_src emacs-lisp
  (elpaca 'marginalia
    (after! 'vertico
      (require 'marginalia)))

  (after! 'marginalia
    (marginalia-mode 1))
#+end_src

** Consult

#+begin_src emacs-lisp
  (elpaca 'consult
    (eval-when-compile
      (require 'consult)))

  (after! 'consult
    (bind-keys ("s-B" . consult-buffer)
               ([remap switch-to-buffer] . consult-buffer)
               ("C-s" . consult-line)
               ("M-g g" . consult-goto-line)))
#+end_src

** Embark

#+begin_src emacs-lisp
  (elpaca 'embark
    (autoload! "embark"
      '(embark-act nil t)
      '(embark-dwim nil t)
      '(embark-bindings nil t))

    (bind-keys ("C-." . embark-act)          ; pick some comfortable binding
               ("C-;" . embark-dwim)         ; good alternative for M-.
               ("C-h B" . embark-bindings))) ; alternative for `describe-bindings'

  (after! 'embark
    ;; FIXME: `org-open-at-point-global' can't open link to heading (in TOC for example)
    ;;
    ;; I solve it just by replacing `org-open-at-point-global' by default
    ;; `org-open-at-point' function when current major mode is Org
    (define-advice org-open-at-point-global
        (:around (orig-fun) current-mode-is-org)
      (if (eq major-mode #'org-mode)
          (funcall #'org-open-at-point)
        (funcall orig-fun))))

  ;; Embark automatically load it after consult is found
  (elpaca 'embark-consult)

  (after! 'embark-consult
    (add-hook 'embark-collect-mode-hook 'consult-preview-at-point-mode))
#+end_src

** Avy

Для навигации в тексте есть множество плагинов:
- =avy= - Основной плагин (и пока что единственный используемый в конфиге)
- =ace-link= - Как =ace-window= или =avy=, но для ссылок
- и.т.д - TODO: Пакетов еще много, их стоит разобрать

Конфигурация =avy=.

TODO: Стоит посмотреть ещё комманды которые предоставляет =avy=. (Это слишком мощная штука)


Почему для =prog-mode= я использую =avy-goto-char-2=, а для =org-mode= - =avy-goto-word-0= !?

Всё довольно просто, в Org Mode я часто пишу на русском, и намного удобнее перемещаться просто по "словам", в то время как
в коде, намного удобнее использовать перемещение по 2 первым символам в "слове".

#+begin_src emacs-lisp
  (elpaca 'avy
    (autoload! "avy"
      '(avy-goto-char-2 nil t)
      '(avy-goto-word-0 nil t))
    (bind-key "C-'" 'avy-goto-char-2 prog-mode-map)
    (after! 'org-mode
      (bind-key* "C-'" 'avy-goto-word-0 org-mode-map)))
#+end_src

** Ace-Window

#+begin_src emacs-lisp
  (elpaca 'ace-window
    (autoload! "ace-window"
      '(ace-window nil t))
    (bind-key "M-o" 'ace-window))
#+end_src

* Editing

** Smartparens

Безальтернативный плагин для автоматического закрытия скобочек (и не только).

В целом данный плагин идеален для редактирования Lisp, Scheme и подобных им языков

#+begin_src emacs-lisp
  (defconst default-pairs-list
    '((?\( . ?\))
      (?\[ . ?\])
      (?\{ . ?\}))
    "List of default pairs")

  (defun open-pair-p (char)
    "Return t if CHAR is opening pair"
    (member char (mapcar (lambda (pairs) (car pairs)) default-pairs-list)))

  (defun close-pair-p (char)
    "Return t if CHAR is closing pair"
    (member char (mapcar (lambda (pairs) (cdr pairs)) default-pairs-list)))

  (defun indent-between-pairs ()
    "Open a new brace or bracket expression, with relevant newlines and indent."
    (interactive)
    (if (and (open-pair-p (char-before))
             (close-pair-p (char-after)))
        (progn (newline)
               (newline)
               (indent-according-to-mode)
               (forward-line -1)
               (indent-according-to-mode))
      (newline-and-indent)))

  (bind-key "RET" 'indent-between-pairs prog-mode-map)

  (elpaca 'smartparens
    (autoload! "smartparens"
      'smartparens-mode
      'smartparens-strict-mode)
    (add-hook 'prog-mode-hook 'smartparens-mode)
    (dolist (hook '(emacs-lisp-mode-hook
                    lisp-mode-hook
                    common-lisp-mode-hook
                    scheme-mode-hook))
      (add-hook hook 'smartparens-strict-mode)))

  (after! 'smartparens
    (require 'smartparens-config)
    (bind-keys :map smartparens-mode-map
               ("M-s" . nil)
               ("M-DEL" . sp-backward-unwrap-sexp)
               ("C-<left>" . sp-forward-barf-sexp)
               ("C-<right>" . sp-forward-slurp-sexp)))
               
#+end_src

** Git-Gutter

#+begin_src emacs-lisp
  (elpaca 'git-gutter
    (autoload 'git-gutter-mode "git-gutter")
    (add-hook 'prog-mode-hook 'git-gutter-mode)
    (add-hook 'text-mode-hook 'git-gutter-mode))
#+end_src

** Tree-Sitter

Tree Sitter в GNU Emacs - это головная боль. Местами он слишком сложен в настройке и просто работает каким-то вообще непонятным образом.

Для начала нужно определить простую функцию которая будет устанавливать грамматику для всех определённых языков.

#+begin_src emacs-lisp
  (defun treesit-install-all ()
    "Install all language grammars from `treesit-language-source-alist'
  variable by `treesit-install-language-grammar' function.

  This function install language grammar only when it unavailable."
    (interactive)
    (mapc
     (lambda (lang)
       (when (not (treesit-language-available-p lang))
         (treesit-install-language-grammar lang)))
     (mapcar #'car treesit-language-source-alist)))
#+end_src

Далее определяем языки (и их грамматику), а так же запускаем их установку.

#+begin_src emacs-lisp
  ;; Tree Sitter source
  (setq treesit-language-source-alist
        '((go "https://github.com/tree-sitter/tree-sitter-go")
          (gomod "https://github.com/camdencheek/tree-sitter-go-mod")
          (c "https://github.com/tree-sitter/tree-sitter-c")
          (zig "https://github.com/maxxnino/tree-sitter-zig")
          (cpp "https://github.com/tree-sitter/tree-sitter-cpp")
          (dockerfile "https://github.com/camdencheek/tree-sitter-dockerfile")
          ;; (yaml "https://github.com/ikatyang/tree-sitter-yaml")
          (python "https://github.com/tree-sitter/tree-sitter-python")
          (bash "https://github.com/tree-sitter/tree-sitter-bash")))

  (run-with-timer 1 nil 'treesit-install-all)
#+end_src

* Completion

** Orderless

#+begin_src emacs-lisp
  (elpaca 'orderless
    (require-on! self-insert-command
                 orderless))

  (after! 'orderless
    (setq completion-styles '(orderless basic)))
#+end_src

** Corfu

#+begin_src emacs-lisp
  (elpaca 'corfu
    (require-on! self-insert-command
                 corfu))

  (after! 'corfu
    (setq corfu-cycle t)
    (setq tab-always-indent 'complete)
    (global-corfu-mode 1)

    (require 'corfu-popupinfo)
    (corfu-popupinfo-mode 1)

    (bind-keys* :map corfu-map
                ("TAB" . corfu-complete)
                ("M-d" . corfu-popupinfo-toggle)
                :map corfu-popupinfo-map
                ("M-n" . corfu-popupinfo-scroll-up)
                ("M-p" . corfu-popupinfo-scroll-down)))
#+end_src

* Org Mode

TODO: =org-ql=
TODO: =org-transclude=

** Main Settings

Базовые переменные

#+begin_src emacs-lisp
  (setq-default org-directory "~/org"
  	      org-id-locations-file (expand-file-name "cache/.org-id-locations" org-directory))
#+end_src

Навигация при помощи =consult-org-heading=.

#+begin_src emacs-lisp
  (after! '(org consult)
    (bind-keys :map org-mode-map
  	     ("C-s" . consult-org-heading)
  	     ("C-S-s" . consult-line)))
#+end_src

Лучшая настройка для таймера.

#+begin_src emacs-lisp
  (after! 'org-timer
    ;; NOTE: I wait until I start using window manager with custom bar (eww, waybar or something similar)
    ;;   When I start using custom bar I can set this variable to `nil'
    (setq org-timer-display 'mode-line)

    (defvar org-timer-active nil
      "Non-nil if org-timer is activated")

    (defun org-timer-activate ()
      "Set `org-timer-active' to t"
      (setq org-timer-active t))

    (defun org-timer-deactivate ()
      "Set `org-timer-active' to nil"
      (setq org-timer-active nil))

    (add-hook 'org-timer-start-hook 'org-timer-activate)
    (add-hook 'org-timer-stop-hook  'org-timer-deactivate)
    (add-hook 'org-timer-set-hook   'org-timer-activate)
    (add-hook 'org-timer-done-hook  'org-timer-deactivate))
#+end_src

Автоматическая генерация [[*Table Of Contents][TOC]].

#+begin_src emacs-lisp
  (elpaca 'toc-org
    (autoload 'toc-org-mode "toc-org")
    (add-hook 'org-mode-hook 'toc-org-mode))
#+end_src

#+begin_src emacs-lisp :noweb-ref org-capture-templates :tangle no
  ("t" "Tasks/Projects")
  ("tt" "Task" entry
   (file "agenda/Inbox.org")
   "* TODO %?\n%U\n%a\n%i"
   :prepend t
   :empty-lines 1)
  ;; ("ts" "Clocked Entry Subtask" entry
  ;;  (clock)
  ;;  "* TODO %?\n%U\n%a\n%i"
  ;;  :prepend t
  ;;  :empty-lines 1)

  ("j" "Journal Entries")
  ("jj" "Journal" entry
   (file+olp+datetree "agenda/Journal.org")
   "\n* %<%I:%M %p> - Journal :journal:\n\n%?\n\n"
   :clock-in :clock-resume
   :empty-lines 1)
  ("jm" "Meeting" entry
   (file+olp+datetree "agenda/Journal.org")
   "* %<%I:%M %p> - %a :meetings:\n\n%?\n\n"
   :clock-in :clock-resume
   :empty-lines 1)

  ;; ("w" "Workflows")
  ;; ("we" "Checking Email" entry (file+olp+datetree ,(dw/get-todays-journal-file-name))
  ;;  "* Checking Email :email:\n\n%?" :clock-in :clock-resume :empty-lines 1)

  ("m" "Metrics Capture")
  ("mw" "Weight" table-line (file+headline "agenda/Metrics.org" "Weight")
   "| %U | %^{Weight} | %^{Notes} |" :kill-buffer t)
#+end_src

#+begin_src emacs-lisp :noweb yes :noweb-prefix no
  (setq-default org-capture-templates '(<<org-capture-templates>>))
  (setq org-todo-keywords
        '((sequence "TODO(t)" "NEXT(n)" "|" "CANC(k@)" "DONE(d!)")))
  (bind-key "o c" 'org-capture mode-specific-map)
#+end_src

** Planning (Agenda)

TODO: Использовать =org-super-agenda=

TODO: article about Emacs+Orgzly+Syncthing

FIXME: Org Mode error with =tab-width= sometimes (i find it only in config.org, don't know about other files and why this problem exists)

Структура Agenda:
- Inbox.org :: Файл для /входящих заметок/
- Tasks.org :: Задачи
- Journal.org :: Журнальчик
- Habits.org :: Привычки и их отслеживание (сюда входят все повторяющиеся задачи)
- Metrics.org :: Метрики (вес, рост, и.т.д)
- *.org :: Остальные файлы описание которых я не предоставляю по каким-либо причинам
# - GetThingsDone.org :: Файл для /входящих задач/

*** Конфигурация

#+begin_src emacs-lisp :noweb yes :noweb-prefix no
  (setq-default org-agenda-custom-commands '(<<custom-agenda-views>>))
#+end_src

#+begin_src emacs-lisp
  (setq-default org-agenda-start-with-log-mode t
                org-log-done 'time
                org-log-into-drawer t)

  (define-advice org-agenda
      (:before (&rest _) update-files)
    (setq org-agenda-files
          (directory-files-recursively
           (expand-file-name "agenda" org-directory)
           "\\`[A-Za-z]*.org\\'")))

  (bind-key "a" 'org-agenda mode-specific-map)
#+end_src

** Note Taking (Roam)

Roam - Идеальная система заметок на базе Org Mode ([[https://ru.wikipedia.org/wiki/%D0%A6%D0%B5%D1%82%D1%82%D0%B5%D0%BB%D1%8C%D0%BA%D0%B0%D1%81%D1%82%D0%B5%D0%BD][Zettelkasten]])

*** Шаблоны (Captures)

**** Daily: Default

#+begin_src emacs-lisp :tangle no :noweb-ref org-roam-daily-captures
  ("d" "default" entry
   "* %?"
   :target (file+head "%<%Y-%m-%d>.org"
                      "#+title: %<%Y-%m-%d>
  ,#+author: Klementiev Dmitry
  ,#+email: klementievd08@yandex.ru
  ,#+date: %<%Y-%m-%d>
  ,#+filetags: :daily:")
   :unnarrowed t)
#+end_src

**** Default

#+begin_src emacs-lisp :tangle no :noweb-ref org-roam-captures
  ("d" "default" plain
   "%?"
   :target (file+head "${slug}.org"
                      "#+title: ${title}
  ,#+author: Klementiev Dmitry
  ,#+email: klementievd08@yandex.ru
  ,#+date: %<%Y-%m-%d>")
   :unarrowed t)
#+end_src

*** Configuration

#+begin_src emacs-lisp :noweb yes :noweb-prefix no
  (setq org-roam-v2-ack t)
  
  (setq-default org-roam-directory (expand-file-name "roam/" org-directory)
                org-roam-db-location (expand-file-name "cache/org-roam.db" org-directory)
                org-roam-dailies-capture-templates '(<<org-roam-daily-captures>>)
                org-roam-capture-templates '(<<org-roam-captures>>))

  (elpaca 'org-roam
    (autoload 'org-roam-db-sync "org-roam")
    (add-hook 'elpaca-after-init-hook
              (lambda ()
                (run-with-timer 1 nil 'org-roam-db-sync))))

  (with-eval-after-load 'org-roam
    (org-roam-db-autosync-mode 1)

    (setq org-roam-db-update-on-save t
          org-roam-dailies-directory "daily/"
          org-roam-node-display-template
          (concat "${title:*} "
                  (propertize "${tags:30}" 'face 'org-tag)))

    (bind-keys :map org-mode-map
               ("C-c r r" . org-roam-ref-add)
               ("C-c r R" . org-roam-ref-remove)
               ("C-c r f" . org-roam-ref-find)
               ("C-c r t" . org-roam-tag-add)
               ("C-c r T" . org-roam-tag-remove)
               ("C-c r a" . org-roam-alias-add)
               ("C-c r A" . org-roam-alias-remove)
               ;; NOTE: See https://git.sr.ht/~abcdw/rde/tree/master/item/src/rde/features/emacs-xyz.scm#L4883
               ;; ("C-c r O" . rde-org-roam-open-ref)
               :map mode-specific-map
               ("n t" . org-roam-dailies-capture-today)
               ("n n" . org-roam-buffer-toggle)
               ("n f" . org-roam-node-find)
               ("n i" . org-roam-node-insert)
               ("n r" . org-roam-ref-find)
               ("n C" . org-roam-capture)))
#+end_src

*** Roam UI

Отличный плагин для просмотра всех заметок и связей между ними.

Красивый веб-фронтенд для заметок Roam.

#+begin_src emacs-lisp
  (elpaca 'org-roam-ui
    ;; Just autoload command for Org Roam UI
    (autoload 'org-roam-ui-mode "org-roam-ui"
      "Web Frontend for Org Roam notes."
      t))
#+end_src

* Development

** VCS (Magit, Forge, Git-Modes)

Git интеграция сводится в основном к двум плагинам: =magit= и =forge=.

=magit= - Незаменимый и безальтернативный Git интерфейс. Более мощного гит интерфейса вы просто не найдёте.

=forge= - Клиент для Github, Gitlab и других хостингов прямо в GNU Emacs.

Пока что я не устанавливаю Forge в своей конфигурации (он мне попросту не нужен, а так же я пишу её не
на своём устройстве). Зато мы сделаем конфиг для Git файлов (gitattributes, gitignore, gitsubmodules)
и настроим Magit, который будет показывать для нас TODOs (плагин =magit-todos=)

#+begin_src emacs-lisp
  (elpaca '(transient
            :host github
            :repo "magit/transient"
            :tag "v0.9.3"))

  (elpaca magit
    (autoload 'magit "magit")
    (bind-key "C-x g" 'magit))
#+end_src

*** Magit TODOs

Плагин который отлично выводит все *TODO* ключевые слова.

#+begin_src emacs-lisp
  (elpaca 'magit-todos
    (autoload 'magit-todos-mode "magit-todos")
    (add-hook 'magit-mode-hook 'magit-todos-mode))
#+end_src

*** Git файлы и режимы для них

#+begin_src emacs-lisp
  (elpaca 'git-modes
    (autoload 'gitignore-mode "git-modes")
    (autoload 'gitconfig-mode "git-modes")
    (autoload 'gitattributes-mode "git-modes")
    (setq auto-mode-alist
          (append
           '((".gitignore\\'" . gitignore-mode)
             (".gitconfig\\'" . gitconfig-mode)
             (".gitattributes\\'" . gitattributes-mode))
           auto-mode-alist)))
#+end_src

** REPL (Eat, Eshell)

Для эмуляции терминала я использую два пакета:
- =eshell= + =eat= - основной эмулятор терминала, который закрывает 90% задач (с режимами от =eat=)
- =eat= - эмулятор терминала закрывающий остальные 10%

*** Eshell

#+begin_src emacs-lisp
  (define-minor-mode eshell-mode-setup
  "Set up environment on `eshell-mode' invocation."
  :group 'eshell
  (if eshell-mode-setup
      (progn
        ;; FIXME: eshell throw error at `completion-at-point' with `all-the-icons-completion-mode' enabled.
        ;; This is just a temporary fix which disable it.
        (when (boundp 'all-the-icons-completion-mode)
          (all-the-icons-completion-mode 0))
        (if (and (boundp 'envrc-global-mode) envrc-global-mode)
            (add-hook 'envrc-mode-hook (lambda () (setenv "PAGER" "")))
          (setenv "PAGER" ""))
        (eshell/alias "l" "ls -al $1")
        (eshell/alias "e" "find-file $1")
        (eshell/alias "ee" "find-file-other-window $1")
        (eshell/alias "d" "dired $1")
        (eshell/alias "gd" "magit-diff-unstaged")
        ;; (local-unset-key 'eshell/clear)
        )
    (when (boundp 'all-the-icons-completion-mode)
      (all-the-icons-completion-mode 1))))
#+end_src

#+begin_src emacs-lisp
  (defun switch-to-prev-buffer-or-eshell (arg)
    (interactive "P")
    (if arg
        (eshell arg)			; or `project-eshell-or-eshell'
      (switch-to-buffer (other-buffer (current-buffer) 1))))

  (defun project-eshell-or-eshell (&optional arg)
    (interactive "P")
    (if (project-current)
        (project-eshell)
      (eshell arg)))
#+end_src

#+begin_src emacs-lisp
  (after! 'eshell
    (require 'em-alias)
    (require 'em-hist)
    
    (add-hook 'eshell-mode-hook 'eshell-mode-setup)

    (bind-key "s-e" 'switch-to-prev-buffer-or-eshell eshell-mode-map)
    (autoload 'consult-history "consult")
    (bind-key "M-r" 'consult-history eshell-hist-mode-map))

  (bind-key "s-e" 'project-eshell-or-eshell)
#+end_src

*** Eat (Emulate A Terminal)

#+begin_src emacs-lisp
  (defun project-eat-or-eat (&optional arg)
    (interactive "P")
    (if (project-current)
        (eat-project arg)
      (eat)))

  (defun switch-to-prev-buffer-or-eat (arg)
    (interactive "P")
    (if arg
        (eat nil arg)
      (switch-to-buffer (other-buffer (current-buffer) 1))))
#+end_src

#+begin_src emacs-lisp
  ;; Priority: nu (nushell) -> zsh -> fish -> bash -> babashka -> sh
  (setq-default eat-shell (or (executable-find "nu")   ; Nushell
                              (executable-find "zsh")  ; ZShell
                              (executable-find "fish") ; Like ZSH but simpler
                              (executable-find "bash") ; Default Bash
                              (executable-find "bb")   ; Clojure REPL (Without JVM)
                              (executable-find "sh"))) ; You don't have Bash !?

  (setq explicit-shell-file-name eat-shell)

  (setq-default eat-line-input-ring-size 1024
                eat-kill-buffer-on-exit t
                eat-term-scrollback-size nil
                eat-enable-mouse t)

  (elpaca 'eat
    (autoload! "eat"
      '(eat nil t)
      '(eat-eshell-mode nil t)
      '(eat-eshell-visual-command-mode nil t))
    (bind-key "s-E" 'project-eat-or-eat)
    (add-hook 'eshell-load-hook 'eat-eshell-mode)
    (add-hook 'eshell-load-hook 'eat-eshell-visual-command-mode))

  (after! 'eat
    (bind-key "s-E" 'switch-to-prev-buffer-or-eat eat-mode-map))
#+end_src

** Languages

TODO: Clojure (Babashka or JVM)

*** Bash + Tree-Sitter

#+begin_src emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.sh\\'" . bash-ts-mode))

  (defalias 'sh-mode 'bash-ts-mode
    "Bash editing mode powered by tree-sitter (ALIAS).")
#+end_src

*** Python + Tree-Sitter

#+begin_src emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.py\\'" . python-ts-mode))
  (defalias 'python-mode 'python-ts-mode)
#+end_src

*** Emacs Lisp

#+begin_src emacs-lisp
  (elpaca 'highlight-defined
    (autoload! "highlight-defined"
       'highlight-defined-mode)
    (add-hook 'emacs-lisp-mode-hook 'highlight-defined-mode))
#+end_src

*** Common Lisp

TODO: Setup completion with Corfu

#+begin_src emacs-lisp
  (setq-default inferior-lisp-program (executable-find "sbcl"))
  (elpaca 'sly)
#+end_src

*** Go + Tree-Sitter

#+begin_src emacs-lisp
  (add-to-list 'auto-mode-alist '("\\.go\\'" . go-ts-mode))
  (add-to-list 'auto-mode-alist '("go.mod\\'" . go-mod-ts-mode))

  (elpaca 'ob-go
    (require-on! org-mode-hook
  	       ob-go))

  (defalias 'go-mode 'go-ts-mode)

  (after! 'go-ts-mode
    (setq go-ts-mode-indent-offset 4))
#+end_src

* Tools

TODO: =docker=
TODO: =kubernetes= or =kubel= or =kele=

** LeetCode

LeetCode клиент для GNU Emacs.

Данный пакет - возможность удобно практиковать алгоритмы и структуры данных, а так же решать задачи прямо в Emacs.

#+begin_src emacs-lisp
  (setq-default leetcode-directory "~/leetcode")

  (elpaca 'leetcode
    (autoload 'leetcode "leetcode"
      "Run LeetCode client for GNU Emacs."
      t))

  (with-eval-after-load 'leetcode
    (setq leetcode-save-solutions t
          leetcode-prefer-language "golang"
          leetcode-prefer-sql "postgresql"))
#+end_src

** Google Translate

#+begin_src emacs-lisp
  (setq-default google-translate-translation-directions-alist
  	      '(("ru" . "en")
  		("en" . "ru")))

  (elpaca 'google-translate
    (autoload! "google-translate-smooth-ui"
      'google-translate-smooth-translate)

    (bind-key "C-c t" 'google-translate-smooth-translate))
#+end_src

** Syncthing

#+begin_src emacs-lisp
  (elpaca 'syncthing
    (autoload! "syncthing"
      '(syncthing nil t)))
#+end_src
